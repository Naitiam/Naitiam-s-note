## PL/SQL

有两种注释:

--

/* */ 

```
declare 
在此声明 常量(constant),变量(variable) 游标
begin
程序的主要部分
exception 
错误处理 
end;
程序结束
```

标识符必须以字母开头，不分大小写。

一般常量可以以C_ 打头命名,变量以V_打头。

### 变量类型 

%type  用于使一个变量的数据类型参照已经定义了的变量。

%rowtype 用于返回一个记录类型, 其数据类型和数据库表的数据结构相一致。

声明变量

```
变量名 类型(长度);
```

变量赋值

```
变量名 类型(长度):=变量值
```

案例：

输出  'hello Oracle11g'

```
set serveroutput on;
-- 将 SQL * PLUS 的环境参数 SERVEROUTPUT 设置为 ON, 否则将看不到输出结果
declare
    c_n varchar2(10):='Oracle 11g';   --定义常量
begin 
   dbms_output.put_line('hello ' || c_n);  
end;[注意;不能漏]
```

将员工编号作为变量，让用户自己输入员工编号

```
declare
      -- 声明部分
      v_empno emp.empno %type := &no; 
      v_ename emp.ename %type;  -- v_ename变量类型和emp表中的ename类型保持一致 %type
      v_sal  emp.sal %type;
      v_hiredate emp.hiredate %type;   
begin
     -- 执行部分
     select 
        empno,
        ename,
        sal,
        hiredate 
     into v_empno,v_ename,v_sal, v_hiredate
     from emp
     where empno = v_empno;
     -- 输出查询信息
     dbms_output.put_line(v_empno || ',' || v_ename || ',' || v_sal ||','|| v_hiredate); 
exception
     -- 异常部分
    when no_data_found then
      dbms_output.put_line('查无此人'); 
end;
```

输出员工编号为7369的全部员工信息(使用%rowtype)

```
declare
     v_emp emp %rowtype;
begin
     select * into v_emp from emp where empno = 7369;
     dbms_output.put_line(v_emp.ename || ',' || v_emp.job || ',' || v_emp.sal ||','||v_emp.hiredate); 
exception
   when no_data_found then
      dbms_output.put_line('查无此人'); 
end;
```

### 流程控制语句

**控制语句**

查询出编号为7369员工工资,若工资大于3000,输出'high salary',若工资小于1000,输出'low salery',否则输出'just so-so'

```
declare 
    v_sal emp.sal%type;
    v_temp varchar2(20);
    c_empno number(4):=7369; -- 绑定常量
begin
   select sal into v_sal from emp where  empno = c_empno;--将查询到sal赋给v_sal
   if v_sal > 3000 then v_temp:='high salary';
   elsif v_sal <1000 then v_temp:='low salary';
   else v_temp:='just so-so';
   end if;
   dbms_output.put_line(v_temp);
end;
```

输入[0-30], 若在[0,9]之间 输出A,若在[10-19]之间输出B,若在[20-29)之间输出C如是[30] 输出D,其他输出error

```
declare
  v_num number := &shuzi;
begin
  case
    when v_num >=0 and v_num <9 then
      dbms_output.put_line('A');
    when v_num >=10 and v_num <19 then
      dbms_output.put_line('B'); 
    when v_num >=20 and v_num <29 then
      dbms_output.put_line('C');
    when v_num =30 then
      dbms_output.put_line('D');
    else
      dbms_output.put_line('ERROR');    
  end case;
end;
```

**循环语句**

```
LOOP
 要执行的语句;
 EXIT WHEN <条件语句> ; /*条件满足,退出循环语句*/
END LOOP;

WHILE <布尔表达式>
   LOOP
 要执行的语句;
END LOOP;

FOR 循环计数器 IN [ REVERSE ] 下限 .. 上限  
/*
每循环一次,循环变量自动加 1;
使用关键字 REVERSE,循环变量自动减 
(1)跟在 IN REVERSE 后面的数字必须是从小到大的顺序 
(2)而且必须是整数,不能是变量或表达式
 */
LOOP
 要执行的语句;
END LOOP;
```

```sql
--法一
declare
    v_num number(2):=1;
begin
  loop
    dbms_output.put_line(v_num);
    v_num:=v_num+1;
  exit when v_num >10;
  end loop;
end;

--法二
declare
    v_num number(2):=1;
begin
  while v_num <=10
  loop
    dbms_output.put_line(v_num);
    v_num:=v_num+1;
  end loop;
end;

--法三
begin
  for v_num in 1 ..10 loop
    dbms_output.put_line(v_num);
  end loop;
end;
```

**顺序语句**

### 异常处理

> ......

----

过程和函数的唯一区别是函数总向调用者返回数据，而过程则不返回数据。

## 存储函数function

```sql
-- 创建存储函数语法
create [or replace] function 函数名
(
参数1 [ { in| in out }] 参数类型,[参数间,隔开]
参数2 [ { in| out| in out } ] 参数类型 
...
) -- 注意这里没有;号
return 返回结果数据类型 [不要指定大小]   --无;号
{ IS | AS }
声明变量或游标;
begin
函数体;
exception
    处理异常;
end;

--删除存储函数语法
drop function 函数名;
```

定义一个函数,获取给定部门的工资总和,要求部门编号定义为参数,工资总额定义为返回值。

```
create or replace function getSal
(
    v_deptno number
)
return number
is    
     cursor  sal_cursor is select sal from emp where deptno = v_deptno;
     v_sum number(10) :=0;
     v_sal emp.sal%type;
begin
     open sal_cursor;
     fetch sal_cursor into v_sal;
     loop 
       if sal_cursor%found then
          v_sum := v_sum+v_sal;   --赋值一定要用:
          fetch sal_cursor into v_sal;
        end if;
     exit when sal_cursor%notfound;
     end loop;
     close sal_cursor;
     return v_sum;
end;

--执行
declare
   v_deptno emp.deptno%type:=&no;
begin
  dbms_output.put_line(getSal(v_deptno));
end;
```

定义一个函数,获取给定部门的工资总和还有部门员工总数(out型参数),要求部门号定义为参数,工资总额定义为返回值。

```
create or replace function getSalAndCount
(
      v_deptno number,
      v_count  out number
)
return number
is
  v_sum number(10) :=0;
begin
  select sum(sal),count(*) into v_sum,v_count from emp where deptno = v_deptno ;
  return v_sum;
end;


--执行参数传递方式1(值传递)
declare
   v_deptno emp.deptno%type:=&no;
   count1  number(10);
begin  
  dbms_output.put_line(getSalAndCount(v_deptno,count1));
  dbms_output.put_line(count1);
end;

--执行参数传递方式2(引用传递)

declare
   v_deptno emp.deptno%type:=&no;
   count1  number(10);
begin  
  dbms_output.put_line(getSalAndCount(v_deptno=>v_deptno,v_count=>count1));
  dbms_output.put_line(count1);
end;
```

## 存储过程procedure

存储过程没return,可以通过out参数返回多个值

```sql
--创建存储过程
create [or replace] procedure 过程名
(参数1 [ { in| in out }] 参数类型,[参数间,隔开]
 参数2 [ { in| out| in out } ] 参数类型 
 ...)
{ IS | AS }
声明变量或游标;
begin
执行部分;
exception
  处理异常;
end;


--删除存储过程
drop  procedure 过程名;
```

插入一条员工信息

```sql
--插入员工记录
create or replace procedure insert_emp
(
       v_empno emp.empno%type,
       v_ename emp.ename%type,
       v_job emp.job%type,
       v_sal emp.sal%type
)
as
       empno_remaing exception;
       pragma exception_init(empno_remaing,-1);
begin
  insert into emp(empno,ename,job,sal) values(v_empno,v_ename,v_job,v_sal);
  commit;
exception
  when empno_remaing then 
   dbms_output.put_line(sqlcode || ',' || sqlerrm);
end;

--查看存储过程错误
select * from SYS.USER_ERRORS where NAME = upper('add_sal');

--执行方式1
call   insert_emp(7955,'zs','clerk',2000);  
--执行方式2
declare
     v_empno emp.empno%type :=&empno;
     v_ename emp.ename%type :=&ename;
     v_job emp.job%type :=&job;
      v_sal emp.sal%type :=&sal;
begin
  insert_emp(v_empno,v_ename,v_job,v_sal);
end;
```

公司给给定部门员工进行加薪,给工资低于3000的员工加薪5%,给工资在3000~5000中的员工加薪3%,给工资大于5000的员工加薪1%,,为此公司每月多付多少成本(out型)

```
create or replace procedure add_sal
(
      v_deptno emp.deptno%type,
      v_temp  out number
)
is
      cursor emp_cursor is select empno,sal from emp where deptno = v_deptno; 
      v_i number(4,2):=0;    
begin    
      v_temp:=0;
      for c in emp_cursor loop
        if c.sal < 3000 then
          v_i := 0.05;
        elsif c.sal <5000 then 
          v_i := 0.03;
        else v_i :=0.01;
        end if;
        --更新工资
        update emp set sal = sal *(1+v_i) where empno = c.empno;
        v_temp := v_temp+c.sal*v_i;
      end loop;         
end;

--执行
declare 
      v_deptno emp.deptno%type:=&deptno;
      v_temp_sal number(10,2);
begin
      add_sal(v_deptno,v_temp_sal);
      dbms_output.put_line(v_temp_sal);
end;
```

## 触发器trigger

- 触发器在数据库里以独立的对象存储

- 它与存储过程不同的是，存储过程通过其它程序来启动运行或直接启动运行，而触发器是由一个事件来启动运行。即触发器是当某个事件发生时自动地隐式运行

- 触发器不能接收参数


```
create [or replace] trigger 触发器名
{before | after }
{insert | delete | update [of column [, column …]]}
on [schema.] 表名 
[for each row ]
[when condition]
触发器体;

触发事件:例如:insert, update, delete
触发时间:before,after
触发频率:说明触发器内定义的动作被执行的次数.即语句级(statement)触发器和行级(row)触发
for each row :选项说明触发器为行触发器.行触发器和语句触发器的区别表现在:行触发器要求当一个 DML 语句操做影响数据库中的多行数据时,对于其中的每个数据行,只要它们符合触发约束条件,均激活一次触发器.

WHEN 子句:触发约束条件
```